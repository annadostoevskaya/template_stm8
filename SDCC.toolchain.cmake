set(CMAKE_SYSTEM_NAME Generic)
set(CMAKE_SYSTEM_VERSION 1)
set(CMAKE_SYSTEM_PROCESSOR stm8)
set(CMAKE_TRY_COMPILE_TARGET_TYPE STATIC_LIBRARY)

if (WIN32)
  set(TOOLCHAIN_C_COMPILER_EXE sdcc.exe)
  find_program(CMAKE_C_COMPILER ${TOOLCHAIN_C_COMPILER_EXE}
    HINTS
      "$ENV{ProgramFiles}/sdcc/bin"
      "D:/portable/sdcc/bin" # NOTE(annad): Nevermind, My personal dirs structure
    REQUIRED
  )

  cmake_path(GET CMAKE_C_COMPILER PARENT_PATH CMAKE_FIND_ROOT_PATH)
  cmake_path(SET CMAKE_FIND_ROOT_PATH                 NORMALIZE "${CMAKE_FIND_ROOT_PATH}/../")
endif ()

if (UNIX)
  find_program(CMAKE_C_COMPILER sdcc REQUIRED)
  set(CMAKE_FIND_ROOT_PATH  /usr/share/sdcc)
endif()

cmake_path(SET CMAKE_C_STANDARD_INCLUDE_DIRECTORIES NORMALIZE "${CMAKE_FIND_ROOT_PATH}/include/")

set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)

# message(CMAKE_C_COMPILER="${CMAKE_C_COMPILER}")
# message(CMAKE_FIND_ROOT_PATH="${CMAKE_FIND_ROOT_PATH}")
# message(CMAKE_C_STANDARD_INCLUDE_DIRECTORIES="${CMAKE_C_STANDARD_INCLUDE_DIRECTORIES}")
